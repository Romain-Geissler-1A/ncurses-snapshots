#! /bin/sh
# $Id: MKkey_defs.sh,v 1.2 2001/06/16 16:03:01 tom Exp $
##############################################################################
# Copyright (c) 2001 Free Software Foundation, Inc.                          #
#                                                                            #
# Permission is hereby granted, free of charge, to any person obtaining a    #
# copy of this software and associated documentation files (the "Software"), #
# to deal in the Software without restriction, including without limitation  #
# the rights to use, copy, modify, merge, publish, distribute, distribute    #
# with modifications, sublicense, and/or sell copies of the Software, and to #
# permit persons to whom the Software is furnished to do so, subject to the  #
# following conditions:                                                      #
#                                                                            #
# The above copyright notice and this permission notice shall be included in #
# all copies or substantial portions of the Software.                        #
#                                                                            #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR #
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,   #
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL    #
# THE ABOVE COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER      #
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING    #
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER        #
# DEALINGS IN THE SOFTWARE.                                                  #
#                                                                            #
# Except as contained in this notice, the name(s) of the above copyright     #
# holders shall not be used in advertising or otherwise to promote the sale, #
# use or other dealings in this Software without prior written               #
# authorization.                                                             #
##############################################################################
#
# MKkey_defs.sh -- generate function-key definitions for curses.h
#
# Author: Thomas E. Dickey <dickey@herndon4.his.com> 2001
#
# Extract function-key definitions from the Caps file
#
: ${AWK-awk}
DATA=${1-Caps}

data=data$$
trap 'rm -f $data' 0 1 2 5 15
sed -e 's/[	]\+/	/g' < $DATA | sort -n +5 >$data

cat <<EOF
/*
 * These definitions were generated by $0 $DATA
 */
EOF

${AWK-awk} <$data '
function print_cols(text,cols) {
	printf "%s", text
	len = length(text);
	while (len < cols) {
		printf "	"
		len += 8;
	}
}
function decode(keycode) {
	result = 0;
	if (substr(keycode, 1, 2) == "0x") {
		digits="0123456789abcdef";
	} else if (substr(keycode, 1, 1) == "0") {
		digits="01234567";
	} else {
		digits="0123456789";
	}
	while (length(keycode) != 0) {
		digit=substr(keycode, 1, 1);
		keycode=substr(keycode, 2);
		result = result * length(digits) + index(digits, digit) - 1;
	}
	return result;
}

BEGIN	{
	maxkey=0
}

/^#/		{next;}
/^capalias/	{next;}
/^infoalias/	{next;}

$5 != "-" && $6 != "-" {
		if ($5 == "KEY_F(0)" ) {
			printf "#define "
			print_cols("KEY_F0", 16);
			print_cols($6, 16);
			print "/* Function keys.  Space for 64 */";
			printf "#define "
			print_cols("KEY_F(n)", 16);
			print_cols("(KEY_F0+(n))", 16);
			print "/* Value of function key n */"
		} else {
			name=$5
			code=$6
			printf "#define "
			print_cols($5, 16);
			print_cols($6, 16);
			printf "/*"
			for (i = 8; i <= NF; i++)
				printf " %s", $i
			print " */"
		}
		thiskey=decode($6);
		if (thiskey > maxkey)
			maxkey = thiskey;
	}
END	{
		testkey=1;
		bits=1;
		while (testkey < maxkey) {
			bits = bits + 1;
			testkey = (testkey * 2) + 1;
		}
		printf "#define ";
		print_cols("KEY_MAX", 16);
		format = sprintf ("%%0%do", (bits + 2) / 3 + 1);
		result = sprintf (format, testkey);
		print_cols(result, 16);
		print "/* Maximum key value */";
	}
'
