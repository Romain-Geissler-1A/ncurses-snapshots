dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.0)
AC_INIT(src/comp_error.c)
AC_PREFIX_DEFAULT(/usr/local)
AC_PREFIX_PROGRAM(tic)
###	Checks for programs.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_CXX
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB

NC_MAKEFLAGS

###	use option --with-cflags to set default CFLAGS
AC_ARG_WITH(cflags,
	[  --with-CFLAGS=value     set default for \$CFLAGS],
	[cflags=$withval])
if test -z "$cflags"; then
	AC_ARG_WITH(cflags,
		[  --with-cflags=value     (same as --with-CFLAGS)],
		[cflags=$withval])
fi

###	use option --with-datadir=DIR to specify an alternate data-directory
AC_ARG_WITH(datadir,
	[  --with-datadir=DIR      specify alternate terminfo data-directory],
	[datadir=$withval],
	[datadir='$(exec_prefix)/lib/terminfo'])
AC_SUBST(datadir)

###	use option --with-includedir=DIR to specify an alternate includedir
AC_ARG_WITH(includedir,
	[  --with-includedir=DIR   specify alternate include-directory],
	[includedir=$withval],
	[includedir='$(prefix)/include/ncurses'])
AC_SUBST(includedir)

###	use option --with-warnings to turn on all gcc warnings
AC_ARG_WITH(warnings,
	[  --with-warnings         turn on GCC compiler warnings],
	[with_warnings=$withval])
if test -n "$GCC"
then
	if test -n "$with_warnings"
	then
		CFLAGS="$CFLAGS -Wall -Wshadow -Wconversion -Wstrict-prototypes -Wmissing-prototypes"
	fi
fi

###	Checks for libraries.
AC_CHECK_LIB(bsd, gettimeofday)dnl CLIX: bzero, select, gettimeofday

###	Checks for header files.
AC_STDC_HEADERS

dnl These are some other potentially nonportable headers.
dnl AC_CHECK_HEADERS(fcntl.h sys/time.h)
AC_CHECK_HEADERS(termios.h sys/ioctl.h sys/select.h libc.h unistd.h)

###	checks for compiler characteristics
AC_C_CONST
AC_C_INLINE

###	Checks for external-data
NC_ERRNO

###	Checks for library functions.
AC_CHECK_FUNCS(sigaction usleep strerror vsscanf)

if test $ac_cv_func_sigaction = yes; then
AC_MSG_CHECKING(whether sigaction needs _POSIX_SOURCE)
AC_TRY_COMPILE([#include <sys/types.h>
#include <signal.h>], [struct sigaction act;],
  sigact_bad=no, sigact_bad=yes AC_DEFINE(SVR4_ACTION))
fi
AC_MSG_RESULT($sigact_bad)
NC_TYPE_SIGACTION

dnl FIXME what are the failure symptoms?  Something we can test?
AC_MSG_CHECKING(whether tiocgwinsz works)
AC_EGREP_CPP(yes, [#if defined(hpux) || defined(__hpux) || defined(__hpux__)
yes
#endif],
 [AC_DEFINE(BROKEN_TIOCGWINSZ) AC_DEFINE(_HPUX_SOURCE) have_gwin=no],
  have_gwin=yes)
AC_EGREP_CPP(yes, [#if defined(__svr4__)
yes
#endif], [AC_DEFINE(BROKEN_TIOCGWINSZ) have_gwin=no])
AC_MSG_RESULT($have_gwin)

dnl FIXME checks we don't do (but neither does the old Configure script):
dnl -DGOOD_SELECT

dnl We'll do our own -g libraries
CFLAGS=`echo ${CFLAGS} | sed -e s/-g//`

changequote({,})dnl
AC_OUTPUT(Makefile include/config_h c++/Makefile man/Makefile src/Makefile test/Makefile,
{
echo creating include/config.h
rm -f include/config.h
echo '/* generated by configure-script */' >include/config.h
sed	-e '/^ -D/!d' \
	-e 's/ -D/\
#define /g' \
	-e 's/\(#define [A-Za-z_][A-Za-z0-9_]*\)=/\1	/g' \
	-e 's@\\@@g' \
	include/config_h | sort >>include/config.h
echo removing include/config_h
rm include/config_h
}
)
changequote([,])dnl
