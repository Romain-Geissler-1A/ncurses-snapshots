dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.0)
AC_INIT(src/comp_error.c)
AC_PREFIX_DEFAULT(/usr/local)
AC_PREFIX_PROGRAM(tic)
###	Checks for programs.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_CXX
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB

NC_MAKEFLAGS

###	use option --with-cflags to set default CFLAGS
AC_ARG_WITH(cflags,
	[  --with-CFLAGS=value     set default for \$CFLAGS],
	[cflags=$withval])
if test -z "$cflags"; then
	AC_ARG_WITH(cflags,
		[  --with-cflags=value     (same as --with-CFLAGS)],
		[cflags=$withval])
fi

###	use option --with-datadir=DIR to specify an alternate data-directory
AC_ARG_WITH(datadir,
	[  --with-datadir=DIR      specify alternate terminfo data-directory],
	[datadir=$withval],
	[datadir='$(exec_prefix)/lib/terminfo'])
AC_SUBST(datadir)

###	use option --with-includedir=DIR to specify an alternate includedir
AC_ARG_WITH(includedir,
	[  --with-includedir=DIR   specify alternate include-directory],
	[includedir=$withval],
	[includedir='$(prefix)/include/ncurses'])
AC_SUBST(includedir)

###	use option --with-warnings to turn on all gcc warnings
AC_ARG_WITH(warnings,
	[  --with-warnings         turn on GCC compiler warnings],
	[with_warnings=$withval])
if test -n "$GCC"
then
	# gcc 2.5.8 doesn't recognize some other useful options that could
	# be configured if you have gcc 2.6.3:
	# -Wbad-function-cast
	# -Wmissing-declarations
	if test -n "$with_warnings"
	then
		CFLAGS="$CFLAGS -Wall -Winline -Wshadow -Wstrict-prototypes -Wmissing-prototypes"
	fi
fi

###	Checks for libraries.
AC_CHECK_LIB(bsd, gettimeofday)dnl CLIX: bzero, select, gettimeofday

###	Checks for header files.
AC_STDC_HEADERS

dnl These are some other potentially nonportable headers.
dnl AC_CHECK_HEADERS(fcntl.h sys/time.h)
AC_CHECK_HEADERS(termios.h sys/ioctl.h sys/select.h libc.h unistd.h)

###	checks for compiler characteristics
AC_LANG_C
AC_C_CONST
AC_C_INLINE

###	Checks for external-data
NC_ERRNO

###	Checks for library functions.
AC_CHECK_FUNCS(sigaction usleep strerror vsscanf)

if test $ac_cv_func_sigaction = yes; then
AC_MSG_CHECKING(whether sigaction needs _POSIX_SOURCE)
AC_TRY_COMPILE([#include <sys/types.h>
#include <signal.h>], [struct sigaction act;],
  sigact_bad=no, sigact_bad=yes AC_DEFINE(SVR4_ACTION))
fi
AC_MSG_RESULT($sigact_bad)

NC_TYPE_SIGACTION
NC_TIOCGWINSZ

dnl FIXME checks we don't do (but neither does the old Configure script):
dnl -DGOOD_SELECT

dnl We'll do our own -g libraries
CFLAGS=`echo ${CFLAGS} | sed -e s/-g//`

dnl Check for C++ compiler characteristics
AC_LANG_CPLUSPLUS
NC_BOOL_DECL
NC_BOOL_SIZE

### It's not possible to appease gcc 2.6.3's conversion-warnings if we're
### using a 'char' for bools.
if test -n "$with_warnings"; then
	if test -n "$GCC"; then
		if test "$nc_cv_sizeof_bool" != "char"; then
			CFLAGS="$CFLAGS -Wconversion"
		fi
	fi
fi

changequote({,})dnl
AC_OUTPUT(Makefile include/config_h c++/Makefile man/Makefile src/Makefile test/Makefile,{

### Special editing.  We generate config.h directly to allow all filenames
### to be MSDOS-compatible, as well as to make the list of definitions be
### dynamically determined by the configuration script -- a consideration when
### doing type-clean development testing.

echo creating include/config.h
rm -f include/config.h
echo "/* generated by configure-script
 * On host: $SYS_NAME
 */" >include/config.h
sed	-e '/^ -D/!d' \
	-e 's/ -D/\
#define /g' \
	-e 's/\(#define [A-Za-z_][A-Za-z0-9_]*\)=/\1	/g' \
	-e 's@\\@@g' \
	include/config_h | sort >>include/config.h
echo removing include/config_h
rm include/config_h

### Edit curses.h in-place
sed -e "
s/#\\(define\\)[ 	][ 	]*\\(CXX_BUILTIN_BOOL\\).*/#\\1 \\2 $BUILTIN_BOOL/
s/#\\(define\\)[ 	][ 	]*\\(CXX_TYPE_OF_BOOL\\).*/#\\1 \\2 $TYPE_OF_BOOL/
" src/curses.h >conftest.tmp
if cmp -s src/curses.h conftest.tmp 2>/dev/null; then
	echo src/curses.h is unchanged
	rm -f conftest.tmp
else
	echo updating src/curses.h
	rm -f src/curses.h~
	mv src/curses.h src/curses.h~
	mv conftest.tmp src/curses.h
fi
},{
### Special initialization commands, used to pass information from the
### configuration-run into config.status

if test $nc_cv_builtin_bool = yes; then BUILTIN_BOOL=1;else BUILTIN_BOOL=0;fi
TYPE_OF_BOOL=$nc_cv_sizeof_bool

SYS_NAME="`(uname -a || hostname) 2>/dev/null | sed 1q`"
if test -z "\$SYS_NAME"; then SYS_NAME=unknown;fi

})
changequote([,])dnl
